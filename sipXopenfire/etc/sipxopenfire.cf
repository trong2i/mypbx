# Copyright (c) 2012 eZuce, Inc. All rights reserved.
# Contributed to SIPfoundry under a Contributor Agreement

# This software is free software; you can redistribute it and/or modify it under
# the terms of the Affero General Public License (AGPL) as published by the
# Free Software Foundation; either version 3 of the License, or (at your option)
# any later version.

# This software is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE. See the GNU Affero General Public License for more
# details.

#
# sipxopenfire configuration and process management
#

bundle agent sipxopenfire {
  methods:
    sipxopenfire::
      "any" usebundle => sipxopenfire_config;  
    any::
      "any" usebundle => sipxopenfire_running;
  files:	
    !sipxopenfire::
      "$(sipx.OPENFIRE_HOME)/conf/openfire.xml"
        comment => "delete openfire configuration file if service not enabled",
        delete => unlink;
      "$(sipx.OPENFIRE_HOME)/conf/openfire.xml.sipx"
        comment => "delete generated openfire configuration file if service not enabled",
        delete => unlink;  
}

bundle agent sipxopenfire_config {
  vars:
    any::
      "xml_file_restart" slist => {
        "sipxopenfire.xml",
        "multipleldap-openfire.xml"
      };
  files:
    sipxopenfire::
      "$(sipx.OPENFIRE_HOME)/plugins/admin/webapp/WEB-INF/web.xml"
        comment => "modify openfire console props",
        create => "false",
        perms => m("644"),
        edit_line => admin_portal_web_xml,
        classes => if_repaired("restart_openfire");

      # Write out the config information so the plugin can read it.
      "/tmp/sipx.properties"
        comment => "install openfire config $(this.promiser)",
        create => "true",
        perms => m("644"),
        edit_defaults => empty,
        edit_line => sipxopenfire_sipx_properties,
        classes => if_repaired("restart_openfire");

      "$(sipx.SIPX_CONFDIR)/$(xml_file_restart)"
        comment => "install openfire config $(this.promiser)",
        create => "true",
        perms => m("644"),
        copy_from => copy_from_cfdata("$(sipx.location_id)/$(xml_file_restart)"),
        classes => if_repaired("restart_openfire");

      "$(sipx.OPENFIRE_HOME)/resources/security/truststore"
        comment => "openfire authorities",
        create => "false",
        perms => mog(600, "$(sipx.OPENFIRE_USER)", "$(sipx.OPENFIRE_GROUP)"),
        copy_from => copy_from_cfdata("authorities.jks"),
        classes => if_repaired("restart_openfire");

      "$(sipx.OPENFIRE_HOME)/resources/security/keystore"
        comment => "openfire certificates",
        create => "false",
        perms => mog(600, "$(sipx.OPENFIRE_USER)", "$(sipx.OPENFIRE_GROUP)"),
        copy_from => copy_from_cfdata("ssl-openfire.keystore"),
        classes => if_repaired("restart_openfire");

      "/etc/sysconfig/openfire"
        comment => "openfire vars $(this.promiser)",
        perms => m("644"),
        edit_defaults => empty,
        edit_line => openfire_config_contents;
                
      "$(sipx.OPENFIRE_HOME)/conf/log4j.properties"
        comment => "configure sipXopenfire log4j $(this.promiser)",
        create => "true",
        perms => mog("644", "$(sipx.OPENFIRE_USER)", "$(sipx.OPENFIRE_GROUP)"),
        edit_line => sipxopenfire_log4j_properties_contents;
        
      "$(sipx.OPENFIRE_HOME)/lib/log4j.xml"
        comment => "configure openfire server log4j xml $(this.promiser)",
        edit_line => sipxopenfire_log4j_xml_contents;

      # We have to copy file in 2 phases because we use one phase to tell when there's a
      # config change and another update openfire. This is because openfire oddly
      # writes changes back to the conf/openfire.xml
      # Phase 1.
      "$(sipx.OPENFIRE_HOME)/conf/openfire.properties.sipx"
        comment => "install openfire.properties config $(this.promiser)",
        create => "true",
        perms => mog("644", "$(sipx.OPENFIRE_USER)", "$(sipx.OPENFIRE_GROUP)"),
        edit_line => openfire_properties,
        classes => if_repaired("update_openfire_conf");

      "$(sipx.OPENFIRE_HOME)/conf/openfire.xml.sipx"
        comment => "install openfire.xml.sipx $(this.promiser)",
        perms => mog("644", "$(sipx.OPENFIRE_USER)", "$(sipx.OPENFIRE_GROUP)"),
        copy_from => local_cp_by_digest("$(sipx.SIPX_CONFDIR)/openfire/openfire.xml"),
        classes => if_repaired("update_openfire_conf");

    update_openfire_conf::
      # Phase 2.
      "$(sipx.OPENFIRE_HOME)/conf/openfire.properties"
        comment => "install openfire properties $(this.promiser)",
        perms => mog("644", "$(sipx.OPENFIRE_USER)", "$(sipx.OPENFIRE_GROUP)"),
        copy_from => local_cp_by_digest("$(sipx.OPENFIRE_HOME)/conf/openfire.properties.sipx"),
        classes => if_repaired("restart_openfire");

      "$(sipx.OPENFIRE_HOME)/conf/openfire.xml"
        comment => "install openfire xml $(this.promiser)",
        perms => mog("644", "$(sipx.OPENFIRE_USER)", "$(sipx.OPENFIRE_GROUP)"),
        copy_from => local_cp_by_digest("$(sipx.OPENFIRE_HOME)/conf/openfire.xml.sipx"),
        classes => if_repaired("restart_openfire");
}


# TRICKY ALERT : This attempts to edit the admin plugins web.xml. This uses a bit of cf trickery
# around selectinng the xml element with "select_region" then uses "replace_patterns"
# around that to change one value for another.
bundle edit_line admin_portal_web_xml {
  vars:
    ofconsole::
      "console_value" string => "login.jsp,index.jsp?logout=true,setup/index.jsp,setup/setup-*,.gif,.png,error-serverdown.jsp,setup/clearspace-integration-prelogin.jsp";
    !ofconsole::
      # TRICKY ALERT: we make all require authentication which would mean you cannot
      # get to login page to autenticate. there's no such file console_disabled but
      # doesn't matter. we just use it as a placeholder to swap out if console goes on
      "console_value" string => "console_disabled";

  replace_patterns:
    "^\s*(console_disabled|login\.jsp).*$"
      select_region => select_filter("AuthCheck"),
      replace_with => value("$(console_value)");
}

body select_region select_filter(name) {
  select_start => "^\s*<filter-name>$(name)<\/filter-name>\s*$";
  select_end => "^\s*<\/filter>\s*$";
}


bundle edit_line openfire_config_contents {
  insert_lines:
    sipxofconfig::
      "CONFIG_NODE=true";
    !sipxofconfig::
      "CONFIG_NODE=false";
    any::
      "SIP_DOMAIN_NAME=$(sipx.domain)";
      "FQDN=$(sipx.host).$(sipx.net_domain)";
      "source $(sipx.SIPX_LIBEXECDIR)/openfire-config $1";
}

bundle edit_line openfire_properties {
  insert_lines:
    any::
      "$(sipx.SIPX_CFDATA)/$(sipx.location_id)/openfire.properties.part"
        insert_type => "file";
      "xmpp.domain=$(sipx.domain)";
      "xmpp.fqdn=$(sipx.host).$(sipx.net_domain)";
  delete_lines:
    any::
      ".*";
}

bundle edit_line sipxopenfire_sipx_properties {
  insert_lines:	
    "$(sipx.SIPX_CFDATA)/$(sipx.location_id)/sipx.properties.part"
      insert_type => "file";	
    "conf.dir=$(sipx.SIPX_CONFDIR)";
    "lib.dir=$(sipx.SIPX_LIBDIR)";
    "localization.file=$(sipx.SIPX_CONFDIR)/openfire/sipxopenfire-prompts.properties";
    "openfire.home=$(sipx.OPENFIRE_HOME)";
}

bundle agent sipxopenfire_running {
  vars:
    any::
      "service" string => "/etc/init.d/openfire";
      "service_command" string => ".*\s-Dprovider.properties.className=org.jivesoftware.util.FilePropertiesProvider\s.*";

  methods:
    any::
      "any" usebundle => find_service_by_command("$(service_command)","openfire_running","$(sipx.OPENFIRE_USER)");
    !src.sipxopenfire::
      "any" usebundle => rh_chkconfig_status("openfire on");
    !src.!sipxopenfire::
      "any" usebundle => rh_chkconfig_status("openfire off");

  commands:
    (!sipxopenfire|stop_sipxecs).openfire_running::
      "$(service)"
        comment => "stop sipxopenfire",
        args => "stop";

    sipxopenfire.!openfire_running.!stop_sipxecs::
      "$(service)"
        comment => "start sipxopenfire",
        args => "start";

    sipxopenfire.openfire_running.(restart_sipxecs|restart_openfire)::
      "$(service)"
        comment => "restart sipxopenfire",
        args => "restart";
}

body copy_from local_cp_by_digest(filename) {
  source => "$(filename)";
  compare => "digest";
  purge => "true";
}

bundle edit_line sipxopenfire_log4j_properties_contents {
  insert_lines:
    any::
      "$(sipx.SIPX_CFDATA)/$(sipx.location_id)/log4j-openfire.properties.part"
        insert_type => "file";
	  "log4j.rootLogger=warn, file";
      "log4j.appender.file=org.sipfoundry.commons.log4j.SipFoundryAppender";
      "log4j.appender.file.File=$(sipx.OPENFIRE_HOME)/logs/sipxopenfire.log";
      "log4j.appender.file.layout=org.sipfoundry.commons.log4j.SipFoundryLayout";
      "log4j.appender.file.layout.facility=sipXopenfire";
  delete_lines:
    any::
      ".*";
}

bundle edit_line sipxopenfire_log4j_xml_contents {
  vars:
    sipxopenfire::
      "loglevelfile" string => readfile("$(sipx.SIPX_CFDATA)/$(sipx.location_id)/log4j-openfire.properties.part", "100");

  classes:
    "sipxopenfire" expression => regextract("(.*)=(.*)(\n|\r|\r\n)",
                                  "$(loglevelfile)",
                                  "loglevel");
  replace_patterns:
    sipxopenfire::
      "debug|DEBUG" replace_with => loglevel("$(loglevel[2])"),
                  	select_region => rootSection;
	  "info|INFO"  	replace_with => loglevel("$(loglevel[2])"),
			      	select_region => rootSection;
	  "warn|WARN" 	replace_with => loglevel("$(loglevel[2])"),
				  	select_region => rootSection;
	  "error|ERROR" replace_with => loglevel("$(loglevel[2])"),
					select_region => rootSection;
	  "fatal|FATAL" replace_with => loglevel("$(loglevel[2])"),
					select_region => rootSection;
}

body replace_with loglevel(x)
{
  replace_value => "$(x)";
  occurrences => "all";
}

body select_region rootSection
{
  select_start => "^\s*?<root>\s*?$";
  select_end => "^\s*?<appender-ref ref=\"debug-out\" />\s*?$";
}